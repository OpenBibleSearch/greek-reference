buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }

        release {
            def keyStore = 'default'
            def keyStorePassword = ''
            def alias = ''
            def aliasPassword = ''
            gradle.taskGraph.whenReady { taskGraph ->
                if (taskGraph.hasTask(':GreekReference:assembleRelease')) {
                    keyStore = System.console().readLine("\nKey store name: ")
                    keyStorePassword = System.console().readLine("Key store password: ")
                    alias = System.console().readLine("Key alias: ")
                    aliasPassword = System.console().readLine("Key password: ")

                    if (keyStore.size() <= 0) {
                        throw new InvalidUserDataException("You must enter a key store name.")
                    } else if (keyStorePassword.size() <= 0) {
                        throw new InvalidUserDataException("You must enter a key store password.")
                    } else if (alias.size() <= 0) {
                        throw new InvalidUserDataException("You must enter a key alias.")
                    } else if (aliasPassword.size() <= 0) {
                        throw new InvalidUserDataException("You must enter a key password.")
                    }
                }

                storeFile file(keyStore)
                storePassword keyStorePassword
                keyAlias alias
                keyPassword aliasPassword
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:18.0.0'
    compile 'com.readystatesoftware.sqliteasset:sqliteassethelper:+'
}
